import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.3'
}

group 'io.beanvortex'
version '1.6.0'
//sourceCompatibility = '25'
//targetCompatibility = '25'
//mainClassName = 'io.beanvortex.bitkip.BitKip'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.beanvortex.bitkip.BitKip'
    mainModule = 'io.beanvortex.bitkip'
}

javafx {
    version = '25'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing']
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.2.2'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.4.0'
    implementation 'org.xerial:sqlite-jdbc:3.50.3.0'
    implementation 'org.jsoup:jsoup:1.21.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'com.zaxxer:HikariCP:7.0.2'
    implementation "io.helidon.webserver:helidon-webserver:3.2.2"
    implementation "io.helidon.webserver:helidon-webserver-cors:3.2.2"
    implementation 'io.helidon.media:helidon-media-jackson:3.2.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.4.0'
    implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'
    implementation 'io.projectreactor:reactor-core:3.7.9'
    implementation 'io.micrometer:context-propagation:1.1.1'
    implementation 'net.java.dev.jna:jna:5.17.0'
    implementation 'net.java.dev.jna:jna-platform:5.17.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    compileOnly 'org.projectlombok:lombok:1.18.40'
    annotationProcessor 'org.projectlombok:lombok:1.18.40'
    testCompileOnly 'org.projectlombok:lombok:1.18.40'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.40'
}

test {
    useJUnitPlatform()
}

jlink {
    launcher {
        name = 'BitKip'
    }
    jpackage {
        if (OperatingSystem.current().windows) {
            appVersion = version
            installerName = 'BitKip'
            installerOptions = ['--win-menu', '--win-shortcut',
                                '--icon', 'src/main/resources/io/beanvortex/bitkip/icons/logo.ico',
                                "--app-version", version as String,
                                "--win-menu-group", "BitKip",
                                "--description", "Free download manager for desktop",
                                '--vendor', 'BeanVortex']
            imageOptions = ['--icon', 'src/main/resources/io/beanvortex/bitkip/icons/logo.ico']
        }
        if (OperatingSystem.current().macOsX) {
            appVersion = version
            installerName = 'BitKip'
            installerOptions = ['--mac-package-name', 'BitKip',
                                '--icon', 'src/main/resources/io/beanvortex/bitkip/icons/logo.icns',
                                "--app-version", version as String,
                                "--description", "Free download manager for desktop",
                                '--vendor', 'BeanVortex']
            imageOptions = ['--icon', 'src/main/resources/io/beanvortex/bitkip/icons/logo.icns']
        }
    }
    addExtraDependencies 'slf4j'
    mergedModule {
        excludeProvides servicePattern: 'reactor.blockhound.*'
    }
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Main-Class': "io.beanvortex.bitkip.Launcher"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


jlinkZip {
    group = 'distribution'
}